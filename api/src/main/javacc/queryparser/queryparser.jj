options {
  LOOKAHEAD = 1;
}

PARSER_BEGIN(QueryParser)

package com.rbmhtechnology.vind.parser.queryparser;

import org.apache.commons.lang3.StringUtils;

public class QueryParser {
    public Query run() throws ParseException {
        return query();
    }
};

PARSER_END(QueryParser)
SKIP : {
    <" " | "\t" | "\n" | "\r" >
}
TOKEN :
{
  <AND:              "AND">
| <OR:               "OR">
| <NOT:              ("NOT" | "!")>
| <PLUS:             "+">
| <MINUS:            "-" >
//| <WS :              " " | "\t" | "\n" | "\r" >
| <LBRACKET:         "(">
| <RBRACKET:         ")">
| <TERM:             (["0"-"9","A"-"Z","a"-"z","_","-","\u0080"-"\uffff"])+ >
| <FIELD:             (["0"-"9","A"-"Z","a"-"z","_","-"])+ ":" >
| <QUOTE:           "\"">
| <QUOTED_STRING:   <QUOTE> (~["\""])+ <QUOTE> >
}

Query query() :
{Query q = new Query();Clause c;}
{
    ((c=fieldClause(){q.add(c);})+ | c=booleanClause(){q.add(c);})
    { return q;}
}

FieldClause fieldClause() :
{BooleanClause query = null;boolean negated = false;Token field = null;Literal value = null;}
{
    ((<PLUS> | <MINUS>{negated=true;})? field=<FIELD> value=literal())
     //| ( <LBRACKET> query=booleanClause() <RBRACKET> )
  {
    String fieldName = field == null ? null : StringUtils.chop(field.image);
    return query == null ?
        new SimpleTermClause(negated, fieldName, value != null ? value : null) :
        new ComplexTermClause(negated, fieldName, query);}
}

Literal literal() :
{Literal l = new Literal();Token value;}
{
    (value=<TERM>{l.add(value.image);} | value=<QUOTED_STRING>{l.add(value.image);})+
    {return l;}
}

BooleanClause binaryBooleanClause() :
{Clause left;Clause right;Token op;}
{
    <LBRACKET> (left=fieldClause()|left=booleanClause()) ( op=<AND> | op=<OR> ) (right=fieldClause()|right=booleanClause()) <RBRACKET>
    { return new BinaryBooleanClause(op.image, left, right);}
}

BooleanClause unaryBooleanClause() :
{FieldClause clause;Token op;}
{
    (op=<NOT>) <LBRACKET> clause=fieldClause() <RBRACKET>
    { return new UnaryBooleanClause(op.image, clause);}
}

BooleanClause booleanClause() :
{BooleanClause clause;}
{
    (clause=unaryBooleanClause() | clause=binaryBooleanClause())
    {return clause;}
}